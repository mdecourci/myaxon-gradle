ext {

    libs = libsMap(
        apache: [
            commons_io: 'commons-io:commons-io:2.4',
            commons_beanutils: 'commons-beanutils:commons-beanutils:1.9.2',
            commons_lang3: 'org.apache.commons:commons-lang3:3.3.2'
        ],
        db: [
            h2: 'com.h2database:h2:1.3.175',
            mssql: 'com.microsoft.sqlserver:sqljdbc4:4.0'
        ],
        json4s: [
              jackson: 'org.json4s:json4s-jackson_2.10:3.2.5'
        ],
        json_simple: 'com.googlecode.json-simple:json-simple:1.1.1',
        test: [
                junit: 'junit:junit:4.11',
                assertj: 'org.assertj:assertj-core:1.6.1',
                jsonAssertj: 'net.javacrumbs.json-unit:json-unit-assertj:0.0.15',
		jsonPath: 'com.jayway.jsonpath:json-path:0.9.1',
		openpojo: 'com.googlecode.openpojo:openpojo:0.5.1',
                mockito: 'org.mockito:mockito-all:1.9.5'
        ]
    )
}

def libsMap(Map<String, ?> map) {
    def processMap
    processMap = { innerMap ->
        innerMap.each {
            if (it.value instanceof Map) {
                innerMap[it.key] = processMap(it.value)
            }
        }
        innerMap.asImmutable().withDefault {
            throw new InvalidUserDataException("No entry '$it' in dependency map (candidates: ${innerMap.keySet().sort().join(", ")})")
        }
    }
    processMap(map)
}
